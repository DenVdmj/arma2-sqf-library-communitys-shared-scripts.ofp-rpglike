
---sqf---
НПС и их диалоги
Неписи регистрируются в файле "NPC\registry.sqf":

// Имя объекта NPC, файл с разговорами, путь задается относительно папки "NPC\Conversation\<LANG>\"
// <LANG> -- язык вашей локализации
[Object_AnyNPC, "AnyNPCFolder\ConversationFile.sqf"],
// Можно зарегиститровать сразу всю группу члены которой будут использовать один файл разговоров:
[Group_GroupOfNPC, "GroupOfNPCFolder\ConversationFile.sqf"],
// Можно зарегиститровать сразу всех юнитов находящихся на территории триггера, все они будут использовать один файл разговоров:
[TRIG_AnyLocationName, "AnyLocationName\ConversationFile.sqf"]

Кстати, не забывайте, что Вы можете поставить триггер с активацией
на кого угодно, на сторону, или на конкретную группу ботов

Пример файла разговора:

// подключить макросы (тербуется аддон rls, но он вообще требуется)
#include "rls\macro"

[
    // начало фрейма
    "вход", // имя фрейма
    {
        // setAve - макро которое меняет начальный фрейм непися,
        // теперь, при следующем посещении непися мы будем
        // попадать в фрейм "вход. знакомы"
        setAve("вход. знакомы");
        [   // текст непися, строка или массив, если массив, то он выводится в несколько экранов
            "Привет!",
            [ // кнопки ответов
                // "текст", "имя фрейма куда осуществляется переход"
                // При выборе этого ответа будет осуществлен выход из разговора
                ["Пока", "[exit]"],
                // При выборе этого ответа будет осуществлен переход к фрейму "время"
                ["Не подскажете время?", "время"],
                // При выборе этого ответа будет открыто окно торговли
                ["Торговать", "[trade]"]
            ]
        ]
    },
    // конец фрейма
    // еще один фрейм
    "вход. знакомы",
    {
        [
            "Это снова ты?",
            [
                ["Пока", "[exit]"],
                ["Время не подскажете?", "время"],
                ["Торговать", "[trade]"]
            ]
        ]
    },
    "время",
    {
        [
            "Сейчас " + (daytime call preprocessFile "Lib\ru.time.sqf"), // подставляется значение, которое вернула функция "Lib\ru.time.sqf"
            [
                ["Пока", "[exit]"],
                ["Торговать", "[trade]"]
            ]
        ]
    }
]

